;*******************************************************************************
;  MSP430FR57x Demo - TimerB, PWM TB0.1-2, Up Mode, 32KHz ACLK
;
;  Description: This program generates two PWM outputs on P1.4,P1.5 using
;  TimerB configured for up mode. The value in CCR0, 100-1, defines the PWM
;  period and the values in CCR1 and CCR2 the PWM duty cycles. Using 32768Hz
;  ACLK as TACLK, the timer period is ~3ms with a 75% duty cycle on P1.4
;  and 25% on P1.5.
;  ACLK = TACLK =32768Hz, SMCLK = MCLK = 4MHz
;
;
;           MSP430FR5739
;         ---------------
;     /|\|               |
;      | |               |
;      --|RST            |
;        |               |
;        |     P1.4/TA0.1|--> CCR1 - 75% PWM
;        |     P1.5/TA0.2|--> CCR2 - 25% PWM
;
;   Tyler Witt
;   Texas Instruments Inc.
;   August 2011
;   Built with IAR Embedded Workbench v6.0
;******************************************************************************
#include "msp430fr5739.h"
;-------------------------------------------------------------------------------
            RSEG    CSTACK                  ; Define stack segment
;-------------------------------------------------------------------------------
            RSEG    CODE
;-------------------------------------------------------------------------------
RESET       mov.w   #SFE(CSTACK),SP         ; Initialize stackpointer
            mov.w   #WDTPW+WDTHOLD,&WDTCTL  ; Stop WDT
	    mov.w   #BIT4+BIT5,&PJSEL0	    ; XT1 Setup
	    mov.b   #0xA5,&CSCTL0_H
	    bis.w   #DCOFSEL0+DCOFSEL1,&CSCTL1 ; Set max DCO setting
	    mov.w   #SELA_0+SELS_3+SELM_3,&CSCTL2 ; ACLK = XT1; MCLK = DCO
	    mov.w   #DIVA_0+DIVS_1+DIVM_1,&CSCTL3 ; set all dividers
	    bis.w   #XT1DRIVE_0,&CSCTL4
	    bic.w   #XT1OFF,&CSCTL4
OSCFlag	    bic.w   #XT1OFFG,&CSCTL5	    ; Clear XT1 fault flag
	    bic.w   #OFIFG,&SFRIFG1
	    cmp.w   #001h,&OFIFG	    ; Test oscillator fault flag
	    jz	    OSCFlag

	    mov.b   #BIT4+BIT5,&P1DIR	    ; P1.4 and P1.5 output
	    mov.b   #BIT4+BIT5,&P1SEL0	    ; P1.4 and P1.5 options select
	    mov.w   #25-1,&TB0CCR0	    ; PWM Period
	    mov.w   #OUTMOD_7,&TB0CCTL1	    ; CCR1 reset/set
	    mov.w   #18,&TB0CCR1	    ; CCR1 PWM duty cycle
	    mov.w   #OUTMOD_7,&TB0CCTL2	    ; CCR2 reset/set
	    mov.w   #6,&TB0CCR2		    ; CCR2 PWM duty cycle
	    mov.w   #TBSSEL_1+MC_1+TBCLR,&TB0CTL ; ACLK, cont mode, clear TBR
						 ; enable interrupt

Mainloop    bis.w   #LPM3,SR	            ; Enter LPM3
	    nop				    ; for debug
			
;-------------------------------------------------------------------------------
            COMMON  INTVEC                  ; Interrupt Vectors
;-------------------------------------------------------------------------------
            ORG     RESET_VECTOR            ; POR, ext. Reset
            DW      RESET
            END
