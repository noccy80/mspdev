;*******************************************************************************
;   MSP430x42x0 Demo - Timer_A, PWM TA1-2 Up Mode, DCO SMCLK
;
;  Description: This program outputs two PWM signals on P1.2 and P1.3 using
;  Timer_A configured for up mode. The value in CCR0 defines the PWM period
;  and the values in CCR1 and CCR2 the PWM duty cycles. Using SMCLK as TACLK,
;  the timer period is 488us with a 75% duty cycle on P1.2 and 25% on P1.3.
;  ACLK = LFXT1 = 32768Hz, MCLK = SMCLK = default DCO = 32 x ACLK = 1048576Hz
;  //* An external watch crystal between XIN & XOUT is required for ACLK *//	
;
;               MSP430F4270
;            -----------------
;        /|\|              XIN|-
;         | |                 | 32kHz
;         --|RST          XOUT|-
;           |                 |
;           |         P1.2/TA1|--> CCR1 - 75% PWM
;           |         P1.3/TA2|--> CCR2 - 25% PWM
;
;  L. Westlund
;  Texas Instruments Inc.
;  June 2005
;  Built with IAR Embedded Workbench Version: 3.30A
;*****************************************************************************
#include  <msp430x42x0.h>
;------------------------------------------------------------------------------
            ORG     08000h                  ; Program Start
;------------------------------------------------------------------------------
RESET       mov.w   #300h,SP                ; Initialize stackpointer
StopWDT     mov.w   #WDTPW+WDTHOLD,&WDTCTL  ; Stop WDT
SetupFLL    bis.b   #XCAP14PF,&FLL_CTL0     ; Configure load caps
SetupP1     bis.b   #00Ch,&P1DIR            ; P1.2,3 output
            bis.b   #00Ch,&P1SEL            ; P1.2 TA1 option, P1.3 TA2
SetupC0     mov.w   #512-1,&CCR0            ; PWM Period
SetupC1     mov.w   #OUTMOD_7,&CCTL1        ; CCR1 reset/set
            mov.w   #384,&CCR1              ; CCR1 PWM Duty Cycle	
SetupC2     mov.w   #OUTMOD_7,&CCTL2        ; CCR2 reset/set
            mov.w   #128,&CCR2              ; CCR2 PWM duty cycle	
SetupTA     mov.w   #TASSEL_2+MC_1,&TACTL   ; SMCLK, upmode
                                            ;					
Mainloop    bis.w   #CPUOFF,SR              ; CPU off
            nop                             ; Required only for debugger
                                            ;
;------------------------------------------------------------------------------
;           Interrupt Vectors Used MSP430x4xx
;------------------------------------------------------------------------------
            ORG     0FFFEh                  ; MSP430 RESET Vector
            DW      RESET                   ;
            END
